plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
    id 'signing'
}

group 'de.dfki.lt.jtok'
version '1.10.0-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    api group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}

java {
    sourceCompatibility = 1.6
    withJavadocJar()
    withSourcesJar()
}

application {
    mainClass = 'de.dfki.lt.tools.tokenizer.JTok'
    applicationName = 'tokenize'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'JTok'
                description = 'A Java-based configurable tokenizer and sentence splitter'
                organization {
                    name = 'Multilinguality and Language Technology (MLT) research department of the German Research Center for Artificial Intelligence, DFKI GmbH'
                    url = 'https://www.dfki.de/web/forschung/forschungsbereiche/sprachtechnologie-und-multilingualitaet/'
                }
                licenses {
                    license {
                        name = 'GNU Lesser General Public License (LGPL) in version 2.1 or higher'
                        url = 'http://www.gnu.org/licenses/#LGPL'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'steffen'
                        name = 'Joerg Steffen'
                        email = 'steffen@dfki.de'
                        url = 'https://www.dfki.de/web/ueber-uns/mitarbeiter/person/jost02/'
                        organization = 'German Research Center for Artificial Intelligence (DFKI)'
                        organizationUrl = 'https://www.dfki.de/'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/DFKI-MLT/JTok'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/DFKI-MLT/JTok/issues'
                }
            }
        }
    }
    repositories {
        maven {
            name 'OSSRH'
            url isReleaseVersion ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                    : 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.maven
}

tasks.withType(Sign).configureEach {
    onlyIf { isReleaseVersion }
}
